buildscript {
	ext {
		name = 'CakeParse'
		kotlinVersion = '1.0.0-rc-1036'
		cucumberVersion = '1.2.4'
		cakeParseVersion = '1.0.1'
		junitVersion = '4.12'
	}
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

plugins {
	id "com.jfrog.bintray" version "1.6"
}

group = 'me.sargunvohra.lib'
version = '1.0.2'

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: 'maven-publish'

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
	
	testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlinVersion
	testCompile group: 'info.cukes', name: 'cucumber-junit', version: cucumberVersion
	testCompile group: 'info.cukes', name: 'cucumber-java8', version: cucumberVersion
}

Properties properties = new Properties()
if(project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {   
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
	
	configurations = ['archives']
	
    pkg {
        repo = 'maven'
        name = name
		desc = 'Simple recursive descent parser combinator library for Kotlin.'
		websiteUrl = 'http://sargunvohra.me/CakeParse/'
		issueTrackerUrl = 'https://github.com/sargunster/CakeParse/issues'
		vcsUrl = 'https://github.com/sargunster/CakeParse.git'
		licenses = ['Apache-2.0']
		labels = ['kotlin', 'parser', 'combinator', 'library']
		publicDownloadNumbers = true
		publish = true
		version {
			name = project.version
			released = new Date()
		}
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact jar
			artifact sourcesJar
			artifact javadocJar
            groupId project.group
            artifactId name
            version project.version
        }
    }
}